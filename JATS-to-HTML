<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    exclude-result-prefixes="xs"
    version="2.0">
    <xsl:output method="html" indent="yes" encoding="UTF-8"/>
    <xsl:strip-space elements="*"/>
    
    <xsl:variable name="var-title">
        <xsl:value-of select="article/front/article-meta/title-group/alt-title"/>
    </xsl:variable>
    <xsl:variable name="var-links">
        <link rel="stylesheet" type="text/css" href="/suppl/css/bootstrap.min.css"/>
        <link rel="stylesheet" type="text/css" href="/suppl/css/bootstrap-theme.min.css"/>
        <link rel="stylesheet" type="text/css" href="/suppl/css/psychosomatics.css"/>
    </xsl:variable>
    <xsl:variable name="var-scripts">
        <script type="text/javascript" src="/suppl/js/jquery.min.js"></script>
        <script type="text/javascript" src="/suppl/js/bootstrap.min.js"></script>
        <script type="text/javascript" src="/suppl/js/psychosomatics.js" defer="defer"></script>
    </xsl:variable>
    <xsl:variable name="var-description">
        <xsl:value-of select="article/front/article-meta/abstract/sec[1]/p"/>
    </xsl:variable>
    <xsl:variable name="var-keywords">
        <xsl:for-each select="article/front/article-meta/kwd-group/kwd">
            <xsl:value-of select="."/>
            <xsl:text>,</xsl:text>
        </xsl:for-each>
    </xsl:variable>
    <xsl:template name="tem-citation_author">
        <xsl:for-each select="article/front/article-meta/contrib-group/contrib">
            <xsl:variable name="var-citation_author">
                <xsl:value-of select="name/given-names"/>
                <xsl:text> </xsl:text>
                <xsl:value-of select="name/surname"/>
            </xsl:variable>
            <xsl:variable name="var-affiliations">
                <xsl:for-each select="xref">
                    <xsl:variable name="var-xref">
                        <xsl:value-of select="./@rid"/>
                    </xsl:variable>
                    <xsl:for-each select="../../aff">
                        <xsl:if test="./@id = $var-xref">
                            <xsl:variable name="var-citation_author_institution">
                                <xsl:value-of select="institution"/>
                                <xsl:text>, </xsl:text>
                                <xsl:value-of select="addr-line/named-content"/>
                                <xsl:text>, </xsl:text>
                                <xsl:value-of select="country"/>
                            </xsl:variable>
                            <meta name="citation_author_institution" content="{$var-citation_author_institution}"/>
                        </xsl:if>
                    </xsl:for-each>
                </xsl:for-each>
            </xsl:variable>
            <meta name="citation_author" content="{$var-citation_author}"/>
            <xsl:copy-of select="$var-affiliations"/>
        </xsl:for-each>
    </xsl:template>
    <xsl:variable name="var-citation_title">
        <xsl:value-of select="article/front/article-meta/title-group/article-title"/>
    </xsl:variable>
    <xsl:variable name="var-itemprop">
        <xsl:value-of select="article/front/article-meta/title-group/article-title"/>
        <xsl:text> (</xsl:text>
        <xsl:value-of select="article/front/article-meta/title-group/subtitle"/>
        <xsl:text>)</xsl:text>
    </xsl:variable>
    <xsl:variable name="var-citation_journal_title">
        <xsl:value-of select="article/front/journal-meta/journal-id"/>
    </xsl:variable>
    <xsl:variable name="var-citation_journal_abbrev">
        <xsl:text>PMGP</xsl:text>
    </xsl:variable>
    <xsl:variable name="var-citation_date">
        <xsl:value-of select="article/front/article-meta/pub-date/year"/>
        <xsl:text>-</xsl:text>
        <xsl:value-of select="article/front/article-meta/pub-date/month"/>
        <xsl:text>-</xsl:text>
        <xsl:value-of select="article/front/article-meta/pub-date/day"/>
    </xsl:variable>
    <xsl:variable name="var-citation_firstpage">
        <xsl:value-of select="article/front/article-meta/elocation-id"/>
    </xsl:variable>
    <xsl:variable name="var-citation_issue">
        <xsl:value-of select="article/front/article-meta/issue"/>
    </xsl:variable>
    <xsl:variable name="var-citation_volume">
        <xsl:value-of select="article/front/article-meta/volume"/>
    </xsl:variable>
    <xsl:variable name="var-citation_issn">
        <xsl:value-of select="article/front/journal-meta/issn"/>
    </xsl:variable>
    <xsl:variable name="var-citation_publisher">
        <xsl:value-of select="article/front/journal-meta/publisher/publisher-name"/>
    </xsl:variable>
    <xsl:variable name="var-citation_fulltext_html_url">
        <xsl:value-of select="article/front/article-meta/artitcle-id/@pub-id-type"/>
    </xsl:variable>
    <xsl:variable name="var-citation_xml_url">
        <xsl:value-of select="article/front/article-meta/artitcle-id/@pub-id-type"/>
    </xsl:variable>
    <xsl:template name="tem-citation_reference">
        <xsl:for-each select="article/back/ref-list/ref">
            <xsl:variable name="var-citation_title2">
                <xsl:text>citation_title=</xsl:text>
                <xsl:value-of select="element-citation/article-title"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <xsl:variable name="var-citation_author">
                <xsl:for-each select="element-citation/person-group/name">
                    <xsl:text>citation_author=</xsl:text>
                    <xsl:value-of select="given-names"/>
                    <xsl:text> </xsl:text>
                    <xsl:value-of select="surname"/>
                    <xsl:text>;</xsl:text>
                </xsl:for-each>
            </xsl:variable>
            <xsl:variable name="var-citation_journal_title2">
                <xsl:text>citation_journal_title=</xsl:text>
                <xsl:value-of select="element-citation/source"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <xsl:variable name="var-citation_volume2">
                <xsl:text>citation_volume=</xsl:text>
                <xsl:value-of select="element-citation/volume"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <xsl:variable name="var-citation_number">
                <xsl:text>citation_number=</xsl:text>
                <xsl:value-of select="element-citation/issue"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <xsl:variable name="var-citation_first_page">
                <xsl:text>citation_first_page=</xsl:text>
                <xsl:value-of select="element-citation/fpage"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <xsl:variable name="var-citation_last_page">
                <xsl:text>citation_last_page=</xsl:text>
                <xsl:value-of select="element-citation/lpage"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <xsl:variable name="var-citation_publication_date">
                <xsl:text>citation_publication_date=</xsl:text>
                <xsl:value-of select="element-citation/year"/>
                <xsl:text>;</xsl:text>
            </xsl:variable>
            <meta name="citation_reference" content="{$var-citation_title2}{$var-citation_author}{$var-citation_journal_title2}{$var-citation_volume2}{$var-citation_number}{$var-citation_first_page}{$var-citation_last_page}{$var-citation_publication_date}"/>
        </xsl:for-each>
    </xsl:template>
    <xsl:variable name="var-DC_Date_created"/>
    <xsl:variable name="var-DC_Date_dateSubmitted"/>
    <xsl:variable name="var-DC_Date_modified"/>
    <xsl:variable name="var-DC_Date_issued">
        <xsl:value-of select="$var-citation_date"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Description"/>
    <xsl:variable name="var-DC_Identifier">
        <xsl:value-of select="$var-citation_fulltext_html_url"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Identifier_pageNumber">
        <xsl:value-of select="article/front/article-meta/elocation-id"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Language">
        <xsl:value-of select="article/@xml:lang"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Rights1">
        <xsl:text>Copyright </xsl:text>
        <xsl:value-of select="article/front/article-meta/permissions/copyright-statement"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Rights2">
        <xsl:value-of select="article/front/article-meta/permissions/ext-link/@xlink:href" xmlns:xlink="http://www.w3.org/1999/xlink"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Source">
        <xsl:value-of select="$var-citation_journal_title"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Source_Issue">
        <xsl:value-of select="$var-citation_issue"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Source_Volume">
        <xsl:value-of select="$var-citation_volume"/>
    </xsl:variable>
    <xsl:variable name="var-DC_Title">
        <xsl:value-of select="$var-itemprop"/>
    </xsl:variable>
    <xsl:template name="article-front" match="article/front"/>
    <xsl:template name="article-body" match="article/body">
        <xsl:apply-templates/>
    </xsl:template>
    <xsl:template match="sec">
        <xsl:choose>
            <xsl:when test="parent::abstract"/>
            <xsl:when test="parent::body">
                <div class="panwrap">
                    <div class="section">
                        <h2 class="title">
                            <xsl:value-of select="title"/>
                        </h2>
                    </div>
                    <div class="forpan">
                        <div class="panel-body">
                            <xsl:apply-templates/>
                        </div>
                    </div>
                </div>
            </xsl:when>
            <xsl:when test="parent::sec">
                <div class="subsection">
                    <xsl:apply-templates/>
                </div>
            </xsl:when>
        </xsl:choose>
    </xsl:template>
    <xsl:template match="title">
        <!-- <xsl:choose>
            <xsl:when test="parent::fn-group"></xsl:when>
        </xsl:choose> -->
        <xsl:choose>
            <xsl:when test="name(../..) = 'abstract'"/>
            <xsl:when test="parent::kwd-group"/>
            <xsl:when test="name(../..) = 'body'"/>
            <xsl:when test="name(../..) = 'sec'">
                <h3 class="subtitle">
                    <xsl:value-of select="."/>
                </h3>
            </xsl:when>
            <xsl:when test="parent::caption">
                <xsl:apply-templates/>
            </xsl:when>
            <xsl:when test="parent::ref-list">
                <xsl:choose>
                    <xsl:when test="title = 'References'">
                        <h2 class="title">
                            <xsl:value-of select="."/>
                        </h2>
                    </xsl:when>
                    <xsl:otherwise>
                        <h2 class="title">
                            <xsl:text>References (</xsl:text>
                            <xsl:value-of select="."/>
                            <xsl:text>)</xsl:text>
                        </h2>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:when>
        </xsl:choose>
    </xsl:template>
    <xsl:template match="p">
        <xsl:choose>
            <xsl:when test="name(../..) = 'abstract'"/>
            <xsl:when test="name(../..) = 'body'">
                <p class="for-sections">
                    <xsl:apply-templates/>
                </p>
            </xsl:when>
            <xsl:when test="name(../..) = 'sec'">
                <p class="for-subsections">
                    <xsl:apply-templates/>
                </p>
            </xsl:when>
            <xsl:when test="parent::list-item">
                <p class="inlist">
                    <xsl:apply-templates/>
                </p>
            </xsl:when>
            <xsl:when test="parent::caption">
                <xsl:apply-templates/>
            </xsl:when>
        </xsl:choose>
    </xsl:template>
    <xsl:template match="xref">
        <xsl:variable name="var-xrefRid">
            <xsl:value-of select="@rid"/>
        </xsl:variable>
        <xsl:choose>
            <xsl:when test="@ref-type = 'bibr'">
                <a class="ref-tip btn btn-info" rid="{$var-xrefRid}">
                    <xsl:value-of select="."/>
                </a>
            </xsl:when>
            <xsl:when test="@ref-type = 'fig'">
                <a href="#{$var-xrefRid}" class="reffigure">
                    <xsl:value-of select="."/>
                </a>
            </xsl:when>
            <xsl:when test="@ref-type = 'table'">
                <a href="#{$var-xrefRid}" class="reftable">
                    <xsl:value-of select="."/>
                </a>
            </xsl:when>
        </xsl:choose>
    </xsl:template>
    <xsl:template match="italic">
        <i>
            <xsl:apply-templates/>
        </i>
    </xsl:template>
    <xsl:template match="bold">
        <b>
            <xsl:apply-templates/>
        </b>
    </xsl:template>
    <xsl:template match="fig">
        <div class="figure-wrap">
            <xsl:variable name="figId">
                <xsl:value-of select="@id"/>
            </xsl:variable>
            <div class="fig-box" id="{$figId}">
                <strong>
                    <xsl:value-of select="label"/>
                    <xsl:text>.</xsl:text>
                </strong>
                <xsl:apply-templates select="caption/title"/>
                <div class="imagewrap">
                    <xsl:variable name="xlink">
                        <xsl:value-of select="graphic/@xlink:href" xmlns:xlink="http://www.w3.org/1999/xlink"/>
                    </xsl:variable>
                    <xsl:variable name="href">
                        <xsl:value-of select="substring-after($xlink, '.com')"/>
                    </xsl:variable>
                    <img src="{$href}"/>
                    <div>
                        <p class="comments">
                            <xsl:apply-templates select="caption/p"/>
                        </p>
                    </div>
                </div>
            </div>
        </div>
    </xsl:template>
    <xsl:template match="table-wrap">
        <div class="figure-wrap">
            <div class="fig-box" id="tbl2">
                <table>
                    <caption class="table-title">
                        <strong>
                            <xsl:value-of select="label"/>
                        </strong>
                        <xsl:apply-templates select="caption/title"/>
                    </caption>
                    <xsl:apply-templates/>
                </table>
                <p class="comments">
                    <xsl:apply-templates select="caption/p"/>
                </p>
            </div>
        </div>
    </xsl:template>
    <xsl:template match="label"/>
    <xsl:template match="caption"/>
    <xsl:template match="table">
        <xsl:apply-templates/>
    </xsl:template>
    <xsl:template match="thead">
        <thead>
            <xsl:apply-templates/>
        </thead>
    </xsl:template>
    <xsl:template match="tbody">
        <tbody>
            <xsl:apply-templates/>
        </tbody>
    </xsl:template>
    <xsl:template match="tr">
        <tr>
            <xsl:apply-templates/>
        </tr>
    </xsl:template>
    <xsl:template match="th">
        <xsl:variable name="col">
            <xsl:value-of select="@colspan"/>
        </xsl:variable>
        <xsl:variable name="row">
            <xsl:value-of select="@rowspan"/>
        </xsl:variable>
        <th colspan="{$col}" rowspan="{$row}">
            <xsl:apply-templates/>
        </th>
    </xsl:template>
    <xsl:template match="td">
        <xsl:variable name="col">
            <xsl:value-of select="@colspan"/>
        </xsl:variable>
        <xsl:variable name="row">
            <xsl:value-of select="@rowspan"/>
        </xsl:variable>
        <td colspan="{$col}" rowspan="{$row}">
            <xsl:apply-templates/>
        </td>
    </xsl:template>
    <xsl:template match="list">
        <xsl:choose>
            <xsl:when test="@list-type = 'ordered'">
                <ol class="intext2">
                    <xsl:apply-templates/>
                </ol>
            </xsl:when>
            <xsl:when test="@list-type = 'unordered'">
                <ul class="intext1">
                    <xsl:apply-templates/>
                </ul>
            </xsl:when>
        </xsl:choose>
    </xsl:template>
    <xsl:template match="list-item">
        <li>
            <xsl:apply-templates/>
        </li>
    </xsl:template>
    <xsl:template name="article-back" match="article/back">
        <div class="panwrap">
            <xsl:apply-templates/>
        </div>
    </xsl:template>
    <xsl:template match="ref-list">
        <div class="section">
            <xsl:apply-templates select="title"/>
        </div>
        <div class="forpan">
            <div class="panel-body">
                <ol class="references">
                    <xsl:apply-templates select="ref"/>
                </ol>
            </div>
        </div>
    </xsl:template>
    <xsl:template match="ref">
        <li class="ref">
            <xsl:variable name="bibId">
                <xsl:value-of select="@id"/>
            </xsl:variable>
            <span class="bib" id="{$bibId}">
                <xsl:apply-templates/>
            </span>
        </li>
    </xsl:template>
    <xsl:template match="element-citation">
        <span class="ref-title">
            <xsl:choose>
                <xsl:when test="article-title">
                    <xsl:value-of select="article-title"/>
                    <xsl:text>. </xsl:text>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="source"/>
                    <xsl:text>. </xsl:text>
                </xsl:otherwise>
            </xsl:choose>
        </span>
        <span class="ref-auth">
            <xsl:apply-templates select="person-group"/>
            <xsl:text>. </xsl:text>
        </span>
        <span class="ref-source">
            <xsl:choose>
                <xsl:when test="@publication-type = 'journal'">
                    <xsl:value-of select="source"/>
                    <xsl:text>. </xsl:text>
                    <xsl:value-of select="year"/>
                    <xsl:text>; </xsl:text>
                    <xsl:value-of select="volume"/>
                    <xsl:text>(</xsl:text>
                    <xsl:value-of select="issue"/>
                    <xsl:text>):</xsl:text>
                    <xsl:value-of select="fpage"/>
                    <xsl:text>-</xsl:text>
                    <xsl:value-of select="lpage"/>
                    <xsl:text>. </xsl:text>
                </xsl:when>
                <xsl:when test="@publication-type = 'conference'">
                    <xsl:value-of select="conf-name"/>
                    <xsl:text>; </xsl:text>
                    <xsl:value-of select="conf-date"/>
                    <xsl:text>, </xsl:text>
                    <xsl:value-of select="year"/>
                    <xsl:text>. </xsl:text>
                    <xsl:value-of select="conf-loc"/>
                    <xsl:text>. </xsl:text>
                </xsl:when>
            </xsl:choose>
        </span>
        <span class="ref-full">
            <xsl:variable name="extLink">
                <xsl:choose>
                    <xsl:when test="pub-id/@pub-id-type = 'doi'">
                        <xsl:text>https://doi.org/</xsl:text>
                        <xsl:value-of select="pub-id"/>
                    </xsl:when>
                    <xsl:when test="pub-id/@pub-id-type = 'pmcid'">
                        <xsl:value-of select="pub-id"/>
                    </xsl:when>
                </xsl:choose>
            </xsl:variable>
            <a class="fulltext" href="{$extLink}">
                <xsl:text>Full Text</xsl:text>
            </a>
        </span>
    </xsl:template>
    <xsl:template match="person-group">
        <xsl:for-each select="name">
            <xsl:value-of select="surname"/>
            <xsl:text> </xsl:text>
            <xsl:value-of select="given-names"/>
            <xsl:if test="position() != last()">
                <xsl:text>, </xsl:text>
            </xsl:if>
        </xsl:for-each>
    </xsl:template>
    
    <xsl:template match="/">
        <xsl:text disable-output-escaping='yes'>&lt;!DOCTYPE html&gt;</xsl:text>
        <html>
            <head>
                <xsl:value-of select="$var-title"/>
                <xsl:copy-of select="$var-links"/>
                <xsl:copy-of select="$var-scripts"/>
                <meta name="viewport" content="width=device-width, initial-scale=1"/>
                <meta name="description" content="{$var-description}"/>
                <meta name="keywords" content="{$var-keywords}"/>
                <meta name="citation_doi" content=""/>
                <xsl:call-template name="tem-citation_author"/>
                <meta name="citation_title" content="{$var-citation_title}"/>
                <meta itemprop="name" content="{$var-itemprop}"/>
                <meta name="citation_journal_title" content="{$var-citation_journal_title}"/>
                <meta name="citation_journal_abbrev" content="{$var-citation_journal_abbrev}"/>
                <meta name="citation_date" content="{$var-citation_date}"/>
                <meta name="citation_firstpage" content="{$var-citation_firstpage}"/>
                <meta name="citation_issue" content="{$var-citation_issue}"/>
                <meta name="citation_volume" content="{$var-citation_volume}"/>
                <meta name="citation_issn" content="{$var-citation_issn}"/>
                <meta name="citation_publisher" content="{$var-citation_publisher}"/>
                <meta name="citation_fulltext_html_url" content="https://e-medjournal.com/index.php/psp/article/view/{$var-citation_fulltext_html_url}"/>
                <meta name="citation_xml_url" content="https://e-medjournal.com/index.php/psp/article/view/{$var-citation_xml_url}"/>
                <xsl:call-template name="tem-citation_reference"/>
                <meta name="DC.Date.created" scheme="ISO8601" content="{$var-DC_Date_created}"/>
                <meta name="DC.Date.dateSubmitted" scheme="ISO8601" content="{$var-DC_Date_dateSubmitted}"/>
                <meta name="DC.Date.issued" scheme="ISO8601" content="{$var-DC_Date_issued}"/>
                <meta name="DC.Date.modified" scheme="ISO8601" content="{$var-DC_Date_modified}"/>
                <meta name="DC.Description" xml:lang="en" content="{$var-DC_Description}"/>
                <meta name="DC.Description" xml:lang="uk" content="{$var-DC_Description}"/>
                <meta name="DC.Format" scheme="IMT" content="application/xml"/>
                <meta name="DC.Format" scheme="IMT" content="text/html"/>
                <meta name="DC.Identifier" content="{$var-DC_Identifier}"/>
                <meta name="DC.Identifier.pageNumber" content="{$var-DC_Identifier_pageNumber}"/>
                <meta name="DC.Identifier.URI" content="https://e-medjournal.com/index.php/psp/article/view/{$var-citation_fulltext_html_url}"/>
                <meta name="DC.Language" scheme="ISO639-1" content="{$var-DC_Language}"/>
                <meta name="DC.Rights" content="{$var-DC_Rights1}"/>
                <meta name="DC.Rights" content="{$var-DC_Rights2}"/>
                <meta name="DC.Source" content="{$var-DC_Source}"/>
                <meta name="DC.Source.Issue" content="{$var-DC_Source_Issue}"/>
                <meta name="DC.Source.Volume" content="{$var-DC_Source_Volume}"/>
                <meta name="DC.Source.URI" content="https://e-medjournal.com/index.php/psp"/>
                <meta name="DC.Title" content="{$var-DC_Title}"/>
                <meta name="DC.Type" content="Text.Serial.Journal"/>
                <meta name="DC.Type.articleType" content="Review"/>
                <meta name="gs_meta_revision" content="1.1"/>
            </head>
            <body data-spy="scroll" data-target="#myAffix">
                <main>
                    <div class="grid-cell">
                        <div class="container-fluid">
                            <div class="row tab-content">
                                <div class="forcontent col-lg-7 col-md-7 col-sm-12 col-xs-12 tab-pane fade in active" role="main" id="article">
                                    <div class="article-content">
                                        <div class="title-block">
                                            <div class="article-meta">
                                                <div class="access-review"></div>
                                                <div class="title-authors-etc">
                                                    <div class="authors-affil"></div>
                                                    <ul class="dates">
                                                        <li class="artdoi"></li>
                                                    </ul>
                                                </div>
                                            </div>
                                        </div>
                                        <div class="article-text">
                                            <div class="front">
                                                <div class="section abstract"></div>
                                                <div class="forpan">
                                                    <div class="panel-body">
                                                        <div class="keywords"></div>
                                                        <div class="abbreviations"></div>
                                                    </div>
                                                </div>
                                            </div>
                                            <xsl:apply-templates select="article/body"/>
                                            <xsl:apply-templates select="article/back"/>
                                        </div>
                                    </div>
                                </div>
                            </div>
                        </div>
                    </div>
                </main>
            </body>
        </html>
    </xsl:template>
</xsl:stylesheet>
